allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    }
    apply plugin: 'java'
    java.sourceCompatibility = JavaVersion.VERSION_1_8
    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'
    compileJava.options.compilerArgs.add '-parameters'
    compileTestJava.options.compilerArgs.add '-parameters'

    version = VERSION
    group = GROUP_ID

    dependencies {
        compileOnly libs.lombok
        annotationProcessor libs.lombok
        testCompileOnly libs.lombok
        testAnnotationProcessor libs.lombok

        testImplementation libs.jupiter.api
        testRuntimeOnly libs.jupiter.engine
        implementation libs.dreamcat.common
    }

    if (file("${projectDir}/gradle.properties").exists()) {
        apply from: new File(rootDir, 'gradle/publish.gradle')
    }
    apply from: new File(rootDir, 'gradle/tasks-jar.gradle')

    test {
        useJUnitPlatform()
    }

    tasks.register('printVersion') {
        doLast {
            print version
        }
    }
}

dependencies {
    // or gradle.properties: org.gradle.java.home=/Library/Java/JavaVirtualMachines/jdk/Contents/Home
    // implementation files(Jvm.current().toolsJar)

    implementation libs.slf4j.api
    testRuntimeOnly libs.slf4j.simple

    implementation libs.bundles.jackson
    implementation libs.bundles.javaparser
    implementation libs.freemarker

    // https://mvnrepository.com/artifact/org.springframework/spring-web
    testImplementation 'org.springframework:spring-web:5.3.31'
    testImplementation 'javax.validation:validation-api:2.0.1.Final'
}

sourceSets {
    test {
        java {
            srcDir 'src/test/java'
            srcDir 'src/test/share'
        }
    }
}